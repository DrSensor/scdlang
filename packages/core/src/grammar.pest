DescriptionFile = { SOI ~ CRLF? ~ (
  NEWLINE* ~ (
    expression
  ) ~ NEWLINE*
)* ~ EOI }

expression = { StateName ~ transition ~ trigger? }
transition = { TransitionTo ~ StateName }
trigger = { TriggerAt ~ EventName }

// #region symbol/operator
TransitionTo = @{ "-"+ ~ ">" }
TriggerAt = @{ "@" }
// #endregion

// #region name
StateName = @{ ASCII_ALPHANUMERIC+ }
EventName = @{ ASCII_ALPHANUMERIC+ }
// #endregion

WHITESPACE = _{ " " | "\t" }
COMMENT = _{ line_comment | block_comment }
line_comment = _{ "//" ~ (!NEWLINE ~ ANY)* }
block_comment = _{ "/*" ~ (block_comment|(!"*/" ~ ANY))* ~ "*/" }

// use for handling data from `stdin`
CRLF = _{
  ("\u{000D}" ~ "\u{000A}")
  | ("\u{000A}" ~ "\u{000D}")
  | "\u{000A}" | "\u{000D}"
  | "\u{001E}"
}
// see https://en.wikipedia.org/wiki/Newline#Representation